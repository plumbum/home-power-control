
compiletoflash

: Flamingo cr
."      _" cr
."     ^-)" cr
."      (.._          .._" cr
."       \`\\        (\`\\        (" cr
."        |>         ) |>        |)" cr
." ______/|________ (7 |` ______\|/_______a:f" cr
;


$40010800 constant PORTA_Base
$40010C00 constant PORTB_Base
$40011000 constant PORTC_Base
$40011400 constant PORTD_Base

PORTA_BASE $08 + constant PORTA_IDR   \ RO              Input Data Register

PORTB_BASE $00 + constant PORTB_CRL   \ Reset $44444444 Port Configuration Register Low
PORTB_BASE $04 + constant PORTB_CRH   \ Reset $44444444 Port Configuration Register High
PORTB_BASE $08 + constant PORTB_IDR   \ RO              Input Data Register
PORTB_BASE $0C + constant PORTB_ODR   \ Reset 0         Output Data Register
PORTB_BASE $10 + constant PORTB_BSRR  \ Reset 0         Port Bit Set/Reset Register
PORTB_BASE $14 + constant PORTB_BRR   \ Reset 0         Port Bit Reset Register
PORTB_BASE $18 + constant PORTB_LCKR  \ Reset 0         Port Configuration Lock Register

PORTC_BASE $00 + constant PORTC_CRL   \ Reset $44444444 Port Configuration Register Low
PORTC_BASE $04 + constant PORTC_CRH   \ Reset $44444444 Port Configuration Register High
PORTC_BASE $08 + constant PORTC_IDR   \ RO              Input Data Register
PORTC_BASE $0C + constant PORTC_ODR   \ Reset 0         Output Data Register
PORTC_BASE $10 + constant PORTC_BSRR  \ Reset 0         Port Bit Set/Reset Register
PORTC_BASE $14 + constant PORTC_BRR   \ Reset 0         Port Bit Reset Register
PORTC_BASE $18 + constant PORTC_LCKR  \ Reset 0         Port Configuration Lock Register

8          constant button1    \ User button on PB8
9          constant button2    \ User button on PB9
1 0 lshift constant led-blue  \ Blue  LED on PB0
1 1 lshift constant led-green \ Green LED on PB1

: init
  $11 PORTB_CRL ! \ Set LEDs as push-pull outputs
  cr
  Flamingo
  cr
  ." Have a nice day !" cr
;

: colours ( -- )
  begin
    key
    dup
    case
      [char] g of led-green portb_odr xor! endof
      [char] b of led-blue  portb_odr xor! endof
    endcase
    27 =
  until
;

\ Systick-Interrupt

: systick ( ticks -- )
    $E000E014 ! \ How many ticks between interrupts ?
  7 $E000E010 ! \ Enable the systick interrupt.
;

: systick-1Hz ( -- ) 8000000 systick ; \ Tick every second with 8 MHz clock


\ Delay with Systick-Timer

$E000E010 constant NVIC_ST_CTRL_R
$E000E014 constant NVIC_ST_RELOAD_R      
$E000E018 constant NVIC_ST_CURRENT_R

: init-delay ( -- )
    \ Start free running Systick Timer without Interrupts
  
    \ Disable SysTick during setup
    0 NVIC_ST_CTRL_R !

    \ Maximum reload value for 24 bit timer
    $00FFFFFF NVIC_ST_RELOAD_R !

    \ Any write to current clears it
    0 NVIC_ST_CURRENT_R !

    \ Enable SysTick with 8MHz clock
    %101 NVIC_ST_CTRL_R ! \ Use %101 instead for core clock
;

: delay-ticks ( ticks -- ) \  Tick = 1/8MHz = 125 ns
  NVIC_ST_CURRENT_R @ \ Get the starting time
  swap -              \ Subtract ticks to wait

  dup 0< if  \ If difference is negative...
           $00FFFFFF and \ Convert to 24-bit subtraction to calculate value after rollover
           begin $800000 NVIC_ST_CURRENT_R bit@ until \ Wait for next rollover
         then

  begin
    dup
    NVIC_ST_CURRENT_R @ \ Get current time
    ( finish finish current )
    u>= \ Systick counts backwards
  until
  drop
;

: us ( us -- ) 8 * delay-ticks ;
: ms ( ms -- ) 0 ?do 8000 delay-ticks loop ;


: button? ( -- ? ) button1 portb_idr bit@ ;

: cornerstone ( Name ) ( -- )
  <builds begin here $3FF and while 0 h, repeat
  does>   begin dup  $3FF and while 2+   repeat 
          eraseflashfrom
;

cornerstone Rewind-to-Basis

compiletoram
init

: readbutton ( -- ) init-delay begin button? . cr 200 ms key? until ;

: tick  ( -- ) ." Tick" cr ;

: clock ( -- ) 
  ['] tick irq-systick !
  systick-1Hz
  eint
;
